// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

import { Ownable } from "@openzeppelin/contracts/access/Ownable.sol";
import { IERC20, SafeERC20 } from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import { IMemberships } from "./interfaces/IMemberships.sol";
import { IMembershipsFactory } from "./interfaces/IMembershipsFactory.sol";
import { IMembershipsProxy, MembershipsProxy } from "./MembershipsProxy.sol";

/// @title MembershipsFactory
/// @notice Factory contract that can deploy Memberships proxies
/// @author Coinvise
contract MembershipsFactory is Ownable, IMembershipsFactory {
    using SafeERC20 for IERC20;

    /// @notice Emitted when trying to set `memberships` to zero address
    error InvalidMemberships();

    /// @notice Emitted when trying to set `feeTreasury` to zero address
    error InvalidFeeTreasury();

    /// @notice Emitted when a proxy is being upgraded by other than proxy owner
    error Unauthorized();

    /// @notice Emitted when performing an invalid upgrade
    /// @param currentVersion current version of Memberships proxy
    /// @param upgradeToVersion version to upgrade the proxy to
    /// @param membershipsLatestVersion latest version of Memberships implementation
    error InvalidUpgrade(uint16 currentVersion, uint16 upgradeToVersion, uint16 membershipsLatestVersion);

    /// @notice Emitted when a Memberships proxy is deployed
    /// @param membershipsProxy address of the newly deployed proxy
    /// @param owner owner of the newly deployed proxy
    /// @param implementation implementation contract used for the newly deployed proxy
    event MembershipsDeployed(address indexed membershipsProxy, address indexed owner, address indexed implementation);

    /// @notice Emitted when a Memberships implementation contract is set for a version
    /// @param version version of implementation
    /// @param implementation implementation contract address
    event MembershipsImplSet(uint16 indexed version, address indexed implementation);

    /// @notice Emitted when feeBPS is changed
    /// @param oldFeeBPS old feeBPS
    /// @param newFeeBPS new feeBPS
    event FeeBPSSet(uint16 oldFeeBPS, uint16 newFeeBPS);

    /// @notice Emitted when fee treasury is changed
    /// @param oldFeeTreasury old fee treasury address
    /// @param newFeeTreasury new fee treasury address
    event FeeTreasurySet(address indexed oldFeeTreasury, address indexed newFeeTreasury);

    /// @notice Fee in basis points
    uint16 public feeBPS;

    /// @notice treasury address to withdraw fees from Memberships
    address payable public feeTreasury;

    /// @notice Mapping to store Memberships implementations versions and addresses: version => membership impl address
    mapping(uint16 => address) internal _membershipsImpls;

    /// @notice Latest version of Memberships implementation
    uint16 public membershipsLatestVersion;

    /// @notice Sets `feeBPS`, `feeTreasury`
    /// @dev Reverts if `_memberships` param is address(0).
    ///      Reverts if `_feeTreasury` param is address(0)
    /// @param _feeBPS fee in bps
    /// @param _feeTreasury treasury address to withdraw fees from Memberships
    constructor(uint16 _feeBPS, address payable _feeTreasury) {
        if (_feeTreasury == address(0)) revert InvalidFeeTreasury();

        feeBPS = _feeBPS;
        feeTreasury = _feeTreasury;
    }

    /// @notice Set Memberships implementation contract for a version.
    ///         Also sets `membershipsLatestVersion` if setting a greater version
    /// @dev Callable only by `owner`.
    ///      Reverts if `_memberships` param is address(0).
    ///      Emits `MembershipsImplSet`
    /// @param _version version of Memberships implementation
    /// @param _memberships address of Memberships implementation contract
    function setMembershipsImplAddress(uint16 _version, address _memberships) external onlyOwner {
        if (_memberships == address(0)) revert InvalidMemberships();

        emit MembershipsImplSet(_version, _memberships);

        _membershipsImpls[_version] = _memberships;
        if (membershipsLatestVersion < _version) membershipsLatestVersion = _version;
    }

    /// @notice Set fee bps
    /// @dev Callable only by `owner`.
    ///      Emits `FeeBPSSet`
    /// @param _feeBPS fee in bps
    function setFeeBPS(uint16 _feeBPS) external onlyOwner {
        emit FeeBPSSet(feeBPS, _feeBPS);

        feeBPS = _feeBPS;
    }

    /// @notice Set fee treasury address
    /// @dev Callable only by `owner`.
    ///      Reverts if `_feeTreasury` param is address(0).
    ///      Emits `FeeTreasurySet`
    /// @param _feeTreasury treasury address to withdraw fees from Memberships
    function setFeeTreasury(address payable _feeTreasury) external onlyOwner {
        if (_feeTreasury == address(0)) revert InvalidFeeTreasury();

        emit FeeTreasurySet(feeTreasury, _feeTreasury);

        feeTreasury = _feeTreasury;
    }

    /// @notice Deploys and initializes a new Membership proxy with the latest implementation
    /// @dev Calls `deployMembershipsAtVersion(membershipsLatestVersion)`
    /// @param _owner Membership owner
    /// @param _treasury treasury address to withdraw sales funds
    /// @param _name name for Membership
    /// @param _symbol symbol for Membership
    /// @param contractURI_ contractURI for Membership
    /// @param baseURI_ baseURI for Membership
    /// @param _membership membership parameters: tokenAddress, price, validity, cap, airdropToken, airdropAmount
    /// @return address of the newly deployed proxy
    function deployMemberships(
        address _owner,
        address payable _treasury,
        string memory _name,
        string memory _symbol,
        string memory contractURI_,
        string memory baseURI_,
        IMemberships.Membership memory _membership
    ) external returns (address) {
        address membershipsProxy = deployMembershipsAtVersion(
            membershipsLatestVersion,
            _owner,
            _treasury,
            _name,
            _symbol,
            contractURI_,
            baseURI_,
            _membership
        );

        return membershipsProxy;
    }

    /// @notice Deploys and initializes a new Membership proxy with the specific implementation version
    /// @dev Only transfers airdrop tokens iff both `_membership.airdropToken` and `_membership.airdropAmount` are set.
    ///      Does not revert if they're invalid.
    ///      Reverts if implementation for `_version` is not set.
    ///      Emits `MembershipsDeployed`
    /// @param _version Memberships implementation version
    /// @param _owner Membership owner
    /// @param _treasury treasury address to withdraw sales funds
    /// @param _name name for Membership
    /// @param _symbol symbol for Membership
    /// @param contractURI_ contractURI for Membership
    /// @param baseURI_ baseURI for Membership
    /// @param _membership membership parameters: tokenAddress, price, validity, cap, airdropToken, airdropAmount
    /// @return address of the newly deployed proxy
    function deployMembershipsAtVersion(
        uint16 _version,
        address _owner,
        address payable _treasury,
        string memory _name,
        string memory _symbol,
        string memory contractURI_,
        string memory baseURI_,
        IMemberships.Membership memory _membership
    ) public returns (address) {
        address membershipsImpl = _membershipsImpls[_version];
        if (membershipsImpl == address(0)) revert InvalidMemberships();

        address membershipsProxy = address(
            new MembershipsProxy(
                _version,
                membershipsImpl,
                abi.encodeWithSelector(
                    IMemberships(membershipsImpl).initialize.selector,
                    _owner,
                    _treasury,
                    _name,
                    _symbol,
                    contractURI_,
                    baseURI_,
                    _membership
                )
            )
        );

        // Transfer airdrop tokens for all memberships
        if (_membership.airdropToken != address(0) && _membership.airdropAmount != 0) {
            IERC20(_membership.airdropToken).safeTransferFrom(
                msg.sender,
                membershipsProxy,
                _membership.airdropAmount * _membership.cap
            );
        }

        emit MembershipsDeployed(membershipsProxy, _owner, membershipsImpl);

        return membershipsProxy;
    }

    /// @notice Upgrade a proxy to latest Memberships implementation
    /// @dev Callable only by proxy owner.
    ///      Reverts if `_version <= currentVersion` or if `_version > membershipsLatestVersion`.
    ///      Reverts if membershipImpl for version is not set
    /// @param _version version to upgrade the proxy to
    /// @param _membershipsProxy address of proxy to upgrade
    function upgradeProxy(uint16 _version, address _membershipsProxy) external {
        if (msg.sender != IMemberships(_membershipsProxy).owner()) revert Unauthorized();

        uint16 currentVersion = IMemberships(_membershipsProxy).version();
        // Only allowing upgrades. So _version should be > current version but <= latest version
        if (_version <= currentVersion || _version > membershipsLatestVersion)
            revert InvalidUpgrade(currentVersion, _version, membershipsLatestVersion);

        address membershipsImpl = _membershipsImpls[_version];
        if (membershipsImpl == address(0)) revert InvalidMemberships();

        IMembershipsProxy(_membershipsProxy).upgradeMemberships(membershipsImpl);
    }

    /// @notice Get Memberships implementation address `version`
    /// @param _version version of Memberships implementation
    /// @return address of Memberships implementation contract for `version`
    function membershipsImpls(uint16 _version) public view returns (address) {
        return _membershipsImpls[_version];
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

interface IMemberships {
    struct Membership {
        address tokenAddress; // token to pay for purchases or renewals
        uint256 price; // price
        uint256 validity; // validity duration in seconds for which a membership is valid after each purchase
        uint256 cap; // total number of memberships
        address airdropToken; // address of the token for airdrop
        uint256 airdropAmount; // number of tokens to airdrop in `airdropToken` decimals
    }

    function owner() external view returns (address);

    function factory() external view returns (address);

    function treasury() external view returns (address payable);

    function price() external view returns (uint256);

    function validity() external view returns (uint256);

    function cap() external view returns (uint256);

    function airdropToken() external view returns (address);

    function airdropAmount() external view returns (uint256);

    function initialize(
        address _owner,
        address payable _treasury,
        string memory _name,
        string memory _symbol,
        string memory contractURI_,
        string memory baseURI_,
        IMemberships.Membership memory _membership
    ) external;

    function pause() external;

    function unpause() external;

    function purchase(address recipient) external payable returns (uint256, uint256);

    function mint(address recipient) external returns (uint256, uint256);

    function renew(uint256 tokenId) external payable returns (uint256);

    function withdraw() external;

    function expirationTimestampOf(uint256 tokenId) external view returns (uint256);

    function isValid(uint256 tokenId) external view returns (bool);

    function hasValidToken(address _owner) external view returns (bool);

    function setDefaultRoyalty(address _receiver, uint96 _feeNumerator) external;

    function contractURI() external view returns (string memory);

    function version() external pure returns (uint16);
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

import { IMemberships } from "./IMemberships.sol";

interface IMembershipsFactory {
    function membershipsLatestVersion() external view returns (uint16);

    function membershipsImpls(uint16 version) external view returns (address);

    function feeBPS() external view returns (uint16);

    function feeTreasury() external view returns (address payable);

    function setMembershipsImplAddress(uint16 _version, address _memberships) external;

    function setFeeBPS(uint16 _feeBPS) external;

    function setFeeTreasury(address payable _feeTreasury) external;

    function deployMemberships(
        address _owner,
        address payable _treasury,
        string memory _name,
        string memory _symbol,
        string memory contractURI_,
        string memory baseURI_,
        IMemberships.Membership memory _membership
    ) external returns (address);

    function deployMembershipsAtVersion(
        uint16 _version,
        address _owner,
        address payable _treasury,
        string memory _name,
        string memory _symbol,
        string memory contractURI_,
        string memory baseURI_,
        IMemberships.Membership memory _membership
    ) external returns (address);

    function upgradeProxy(uint16 _version, address _membershipsProxy) external;
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

import "./interfaces/IMembershipsFactory.sol";
import "./interfaces/IMembershipsProxy.sol";

/// @title MembershipsProxy
/// @notice Proxy contract that will be deployed by MembershipsFactory
/// @author Coinvise
contract MembershipsProxy is ERC1967Proxy, IMembershipsProxy {
    /// @notice Emitted when being called by other than registered factory contract
    error Unauthorized();

    /// @notice Emitted when MembershipsFactory.memberships() is zero address
    error InvalidMemberships();

    /// @dev Storage slot with the factory of the contract.
    ///      This is the keccak-256 hash of "co.coinvise.memberships.factory" subtracted by 1,
    ///      and is validated in the constructor
    bytes32 private constant _FACTORY_SLOT = 0x1ad0e7da7a8ae5b58d3a6900c5b9701c3c2713e4e7cef4d5a30267fc422c6301;

    /// @dev Only factory can call
    modifier onlyFactory() {
        if (msg.sender != _factory()) revert Unauthorized();
        _;
    }

    /// @notice Deploys and initializes ERC1967 proxy contract
    /// @dev Reverts if memberships implementation address not set in MembershipsFactory.
    ///      Sets caller as factory
    /// @param _membershipsVersion version of Memberships implementation
    /// @param _memberships address of Memberships implementation contract
    /// @param _data encoded data to be used to initialize proxy
    constructor(
        uint16 _membershipsVersion,
        address _memberships,
        bytes memory _data
    ) ERC1967Proxy(_memberships, _data) {
        assert(_FACTORY_SLOT == bytes32(uint256(keccak256("co.coinvise.memberships.factory")) - 1));

        if (IMembershipsFactory(msg.sender).membershipsImpls(_membershipsVersion) != _memberships)
            revert InvalidMemberships();

        _setFactory(msg.sender);
    }

    /// @notice Upgrade proxy implementation contract
    /// @dev Callable only by factory
    /// @param _memberships address of membership implementation contract to upgrade to
    function upgradeMemberships(address _memberships) public onlyFactory {
        _upgradeTo(_memberships);
    }

    /// @notice Get implementation contract address
    /// @dev Reads address set in ERC1967Proxy._IMPLEMENTATION_SLOT
    /// @return address of implementation contract
    function memberships() public view returns (address) {
        return _implementation();
    }

    /// @notice Get factory contract address
    /// @dev Reads address set in _FACTORY_SLOT
    /// @return address of factory contract
    function membershipsFactory() public view returns (address) {
        return _factory();
    }

    /// @dev Returns the current factory.
    function _factory() internal view returns (address factory) {
        bytes32 slot = _FACTORY_SLOT;

        // solhint-disable-next-line no-inline-assembly
        assembly {
            factory := sload(slot)
        }
    }

    /// @dev Stores a new address in the EIP1967 factory slot.
    function _setFactory(address factory) private {
        bytes32 slot = _FACTORY_SLOT;

        // solhint-disable-next-line no-inline-assembly
        assembly {
            sstore(slot, factory)
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (proxy/ERC1967/ERC1967Proxy.sol)

pragma solidity ^0.8.0;

import "../Proxy.sol";
import "./ERC1967Upgrade.sol";

/**
 * @dev This contract implements an upgradeable proxy. It is upgradeable because calls are delegated to an
 * implementation address that can be changed. This address is stored in storage in the location specified by
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967], so that it doesn't conflict with the storage layout of the
 * implementation behind the proxy.
 */
contract ERC1967Proxy is Proxy, ERC1967Upgrade {
    /**
     * @dev Initializes the upgradeable proxy with an initial implementation specified by `_logic`.
     *
     * If `_data` is nonempty, it's used as data in a delegate call to `_logic`. This will typically be an encoded
     * function call, and allows initializating the storage of the proxy like a Solidity constructor.
     */
    constructor(address _logic, bytes memory _data) payable {
        assert(_IMPLEMENTATION_SLOT == bytes32(uint256(keccak256("eip1967.proxy.implementation")) - 1));
        _upgradeToAndCall(_logic, _data, false);
    }

    /**
     * @dev Returns the current implementation address.
     */
    function _implementation() internal view virtual override returns (address impl) {
        return ERC1967Upgrade._getImplementation();
    }
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

interface IMembershipsProxy {
    function memberships() external view returns (address);

    function membershipsFactory() external view returns (address);

    function upgradeMemberships(address _memberships) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (proxy/Proxy.sol)

pragma solidity ^0.8.0;

/**
 * @dev This abstract contract provides a fallback function that delegates all calls to another contract using the EVM
 * instruction `delegatecall`. We refer to the second contract as the _implementation_ behind the proxy, and it has to
 * be specified by overriding the virtual {_implementation} function.
 *
 * Additionally, delegation to the implementation can be triggered manually through the {_fallback} function, or to a
 * different contract through the {_delegate} function.
 *
 * The success and return data of the delegated call will be returned back to the caller of the proxy.
 */
abstract contract Proxy {
    /**
     * @dev Delegates the current call to `implementation`.
     *
     * This function does not return to its internal call site, it will return directly to the external caller.
     */
    function _delegate(address implementation) internal virtual {
        assembly {
            // Copy msg.data. We take full control of memory in this inline assembly
            // block because it will not return to Solidity code. We overwrite the
            // Solidity scratch pad at memory position 0.
            calldatacopy(0, 0, calldatasize())

            // Call the implementation.
            // out and outsize are 0 because we don't know the size yet.
            let result := delegatecall(gas(), implementation, 0, calldatasize(), 0, 0)

            // Copy the returned data.
            returndatacopy(0, 0, returndatasize())

            switch result
            // delegatecall returns 0 on error.
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }

    /**
     * @dev This is a virtual function that should be overridden so it returns the address to which the fallback function
     * and {_fallback} should delegate.
     */
    function _implementation() internal view virtual returns (address);

    /**
     * @dev Delegates the current call to the address returned by `_implementation()`.
     *
     * This function does not return to its internal call site, it will return directly to the external caller.
     */
    function _fallback() internal virtual {
        _beforeFallback();
        _delegate(_implementation());
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if no other
     * function in the contract matches the call data.
     */
    fallback() external payable virtual {
        _fallback();
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if call data
     * is empty.
     */
    receive() external payable virtual {
        _fallback();
    }

    /**
     * @dev Hook that is called before falling back to the implementation. Can happen as part of a manual `_fallback`
     * call, or as part of the Solidity `fallback` or `receive` functions.
     *
     * If overridden should call `super._beforeFallback()`.
     */
    function _beforeFallback() internal virtual {}
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (proxy/ERC1967/ERC1967Upgrade.sol)

pragma solidity ^0.8.2;

import "../beacon/IBeacon.sol";
import "../../interfaces/draft-IERC1822.sol";
import "../../utils/Address.sol";
import "../../utils/StorageSlot.sol";

/**
 * @dev This abstract contract provides getters and event emitting update functions for
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967] slots.
 *
 * _Available since v4.1._
 *
 * @custom:oz-upgrades-unsafe-allow delegatecall
 */
abstract contract ERC1967Upgrade {
    // This is the keccak-256 hash of "eip1967.proxy.rollback" subtracted by 1
    bytes32 private constant _ROLLBACK_SLOT = 0x4910fdfa16fed3260ed0e7147f7cc6da11a60208b5b9406d12a635614ffd9143;

    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Emitted when the implementation is upgraded.
     */
    event Upgraded(address indexed implementation);

    /**
     * @dev Returns the current implementation address.
     */
    function _getImplementation() internal view returns (address) {
        return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 implementation slot.
     */
    function _setImplementation(address newImplementation) private {
        require(Address.isContract(newImplementation), "ERC1967: new implementation is not a contract");
        StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
    }

    /**
     * @dev Perform implementation upgrade
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeTo(address newImplementation) internal {
        _setImplementation(newImplementation);
        emit Upgraded(newImplementation);
    }

    /**
     * @dev Perform implementation upgrade with additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCall(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        _upgradeTo(newImplementation);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(newImplementation, data);
        }
    }

    /**
     * @dev Perform implementation upgrade with security checks for UUPS proxies, and additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCallUUPS(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        // Upgrades from old implementations will perform a rollback test. This test requires the new
        // implementation to upgrade back to the old, non-ERC1822 compliant, implementation. Removing
        // this special case will break upgrade paths from old UUPS implementation to new ones.
        if (StorageSlot.getBooleanSlot(_ROLLBACK_SLOT).value) {
            _setImplementation(newImplementation);
        } else {
            try IERC1822Proxiable(newImplementation).proxiableUUID() returns (bytes32 slot) {
                require(slot == _IMPLEMENTATION_SLOT, "ERC1967Upgrade: unsupported proxiableUUID");
            } catch {
                revert("ERC1967Upgrade: new implementation is not UUPS");
            }
            _upgradeToAndCall(newImplementation, data, forceCall);
        }
    }

    /**
     * @dev Storage slot with the admin of the contract.
     * This is the keccak-256 hash of "eip1967.proxy.admin" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;

    /**
     * @dev Emitted when the admin account has changed.
     */
    event AdminChanged(address previousAdmin, address newAdmin);

    /**
     * @dev Returns the current admin.
     */
    function _getAdmin() internal view returns (address) {
        return StorageSlot.getAddressSlot(_ADMIN_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 admin slot.
     */
    function _setAdmin(address newAdmin) private {
        require(newAdmin != address(0), "ERC1967: new admin is the zero address");
        StorageSlot.getAddressSlot(_ADMIN_SLOT).value = newAdmin;
    }

    /**
     * @dev Changes the admin of the proxy.
     *
     * Emits an {AdminChanged} event.
     */
    function _changeAdmin(address newAdmin) internal {
        emit AdminChanged(_getAdmin(), newAdmin);
        _setAdmin(newAdmin);
    }

    /**
     * @dev The storage slot of the UpgradeableBeacon contract which defines the implementation for this proxy.
     * This is bytes32(uint256(keccak256('eip1967.proxy.beacon')) - 1)) and is validated in the constructor.
     */
    bytes32 internal constant _BEACON_SLOT = 0xa3f0ad74e5423aebfd80d3ef4346578335a9a72aeaee59ff6cb3582b35133d50;

    /**
     * @dev Emitted when the beacon is upgraded.
     */
    event BeaconUpgraded(address indexed beacon);

    /**
     * @dev Returns the current beacon.
     */
    function _getBeacon() internal view returns (address) {
        return StorageSlot.getAddressSlot(_BEACON_SLOT).value;
    }

    /**
     * @dev Stores a new beacon in the EIP1967 beacon slot.
     */
    function _setBeacon(address newBeacon) private {
        require(Address.isContract(newBeacon), "ERC1967: new beacon is not a contract");
        require(
            Address.isContract(IBeacon(newBeacon).implementation()),
            "ERC1967: beacon implementation is not a contract"
        );
        StorageSlot.getAddressSlot(_BEACON_SLOT).value = newBeacon;
    }

    /**
     * @dev Perform beacon upgrade with additional setup call. Note: This upgrades the address of the beacon, it does
     * not upgrade the implementation contained in the beacon (see {UpgradeableBeacon-_setImplementation} for that).
     *
     * Emits a {BeaconUpgraded} event.
     */
    function _upgradeBeaconToAndCall(
        address newBeacon,
        bytes memory data,
        bool forceCall
    ) internal {
        _setBeacon(newBeacon);
        emit BeaconUpgraded(newBeacon);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(IBeacon(newBeacon).implementation(), data);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (proxy/beacon/IBeacon.sol)

pragma solidity ^0.8.0;

/**
 * @dev This is the interface that {BeaconProxy} expects of its beacon.
 */
interface IBeacon {
    /**
     * @dev Must return an address that can be used as a delegate call target.
     *
     * {BeaconProxy} will check that this address is a contract.
     */
    function implementation() external view returns (address);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (interfaces/draft-IERC1822.sol)

pragma solidity ^0.8.0;

/**
 * @dev ERC1822: Universal Upgradeable Proxy Standard (UUPS) documents a method for upgradeability through a simplified
 * proxy whose upgrades are fully controlled by the current implementation.
 */
interface IERC1822Proxiable {
    /**
     * @dev Returns the storage slot that the proxiable contract assumes is being used to store the implementation
     * address.
     *
     * IMPORTANT: A proxy pointing at a proxiable contract should not be considered proxiable itself, because this risks
     * bricking a proxy that upgrades to it, by delegating to itself until out of gas. Thus it is critical that this
     * function revert if invoked through a proxy.
     */
    function proxiableUUID() external view returns (bytes32);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/StorageSlot.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for reading and writing primitive types to specific storage slots.
 *
 * Storage slots are often used to avoid storage conflict when dealing with upgradeable contracts.
 * This library helps with reading and writing to such slots without the need for inline assembly.
 *
 * The functions in this library return Slot structs that contain a `value` member that can be used to read or write.
 *
 * Example usage to set ERC1967 implementation slot:
 * ```
 * contract ERC1967 {
 *     bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
 *
 *     function _getImplementation() internal view returns (address) {
 *         return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
 *     }
 *
 *     function _setImplementation(address newImplementation) internal {
 *         require(Address.isContract(newImplementation), "ERC1967: new implementation is not a contract");
 *         StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
 *     }
 * }
 * ```
 *
 * _Available since v4.1 for `address`, `bool`, `bytes32`, and `uint256`._
 */
library StorageSlot {
    struct AddressSlot {
        address value;
    }

    struct BooleanSlot {
        bool value;
    }

    struct Bytes32Slot {
        bytes32 value;
    }

    struct Uint256Slot {
        uint256 value;
    }

    /**
     * @dev Returns an `AddressSlot` with member `value` located at `slot`.
     */
    function getAddressSlot(bytes32 slot) internal pure returns (AddressSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `BooleanSlot` with member `value` located at `slot`.
     */
    function getBooleanSlot(bytes32 slot) internal pure returns (BooleanSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Bytes32Slot` with member `value` located at `slot`.
     */
    function getBytes32Slot(bytes32 slot) internal pure returns (Bytes32Slot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Uint256Slot` with member `value` located at `slot`.
     */
    function getUint256Slot(bytes32 slot) internal pure returns (Uint256Slot storage r) {
        assembly {
            r.slot := slot
        }
    }
}