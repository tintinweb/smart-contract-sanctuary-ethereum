// SPDX-License-Identifier: MIT
pragma solidity ^0.8.9;

// Step
// User create a trade offer from discord
// 1 user click accept offer
// Create a thread
// open nft escrow link

// MARK: - Trade session init
// User A create trade offer with to address
// User B see the trade offer from user A

// MARK: - Send/Withdraw items to/from escrow
// User A send/withdraw nft items to/from escrow
// User B send/withdraw nft items to/from escrow

// MARK: - Lock phase
// User A check if items from B is correct, then lock
// User B check if items from A is correct, then lock

// MARK: - Finish
// After the last lock, item from escrow will be transfered to A and B

// MARK: - Note
// If there is any changes in lock phase, the lock will be unlock and user need to check again

// This version currently just support ERC721
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/Address.sol";

contract MochiNFTEscrow is IERC721Receiver, Pausable, Ownable, ReentrancyGuard {
    struct TradeItem {
        uint256 tradeId;
        address tokenAddress;
        uint256 tokenId;
        bool exists;
    }

    struct TradeOffer {
        address from;
        address to;
        bool isFromLocked;
        bool isToLocked;
        bool isClosed;
    }

    TradeOffer[] public trades;
    TradeItem[] public tradeItems;

    // from/to address => tradeIds
    mapping(address => uint256[]) public userTrades;
    // tradeId => owner address => itemIds
    mapping(uint256 => mapping(address => uint256[])) public userTradingItems;

    event TradeCreated(
        uint256 indexed tradeId,
        address indexed from,
        address indexed to
    );
    event TradeCancelled(uint256 indexed tradeId, address indexed cancelUser);
    event TradeSuccess(
        uint256 indexed tradeId,
        address indexed from,
        address indexed to
    );

    event Deposit(uint256 indexed tradeId, address indexed user);
    event Withdraw(uint256 indexed tradeId, address indexed user);

    event Lock(uint256 indexed tradeId, address indexed user);
    event Unlock(uint256 indexed tradeId, address indexed user);

    constructor() {}

    modifier onlyRelevant(uint256 tradeId) {
        TradeOffer memory trade = trades[tradeId];
        require(
            msg.sender == trade.from || msg.sender == trade.to,
            "you are not buyer or seller"
        );
        _;
    }

    modifier whenTradeOpen(uint256 tradeId) {
        TradeOffer memory trade = trades[tradeId];
        require(trade.isClosed == false, "trade is closed");
        _;
    }

    modifier notContract() {
        require(!Address.isContract(msg.sender), "not allow contract");
        _;
    }

    function createTradeOffer(address to) external notContract whenNotPaused {
        trades.push(TradeOffer(msg.sender, to, false, false, false));
        uint256 tradeId = trades.length - 1;
        userTrades[msg.sender].push(tradeId);
        userTrades[to].push(tradeId);
        emit TradeCreated(tradeId, msg.sender, to);
    }

    function cancelTradeOffer(uint256 tradeId)
        external
        notContract
        whenNotPaused
        whenTradeOpen(tradeId)
        onlyRelevant(tradeId)
    {
        TradeOffer storage trade = trades[tradeId];
        _withdrawAll(tradeId, trade.from);
        _withdrawAll(tradeId, trade.to);
        trade.isClosed = true;
        emit TradeCancelled(tradeId, msg.sender);
    }

    function lockTrade(uint256 tradeId)
        public
        notContract
        whenNotPaused
        whenTradeOpen(tradeId)
        onlyRelevant(tradeId)
        nonReentrant
    {
        TradeOffer storage trade = trades[tradeId];
        if (msg.sender == trade.from) {
            trade.isFromLocked = true;
        } else {
            trade.isToLocked = true;
        }
        emit Lock(tradeId, msg.sender);
        // check if both locked, then swap items
        if (trade.isFromLocked && trade.isToLocked) {
            _swapItems(tradeId);
            // Finished, close the trade
            trade.isClosed = true;
            emit TradeSuccess(tradeId, trade.from, trade.to);
        }
    }

    function _swapItems(uint256 tradeId) internal {
        TradeOffer memory trade = trades[tradeId];
        // Send 'from' items to 'to' address
        uint256[] memory fromItemIds = userTradingItems[tradeId][trade.from];
        for (uint256 i = 0; i < fromItemIds.length; i++) {
            TradeItem memory item = tradeItems[fromItemIds[i]];
            IERC721(item.tokenAddress).approve(msg.sender, item.tokenId);
            IERC721(item.tokenAddress).safeTransferFrom(
                address(this),
                trade.to,
                item.tokenId
            );
        }
        // Send 'to' items to 'from' address
        uint256[] memory toItemIds = userTradingItems[tradeId][trade.to];
        for (uint256 i = 0; i < toItemIds.length; i++) {
            TradeItem memory item = tradeItems[toItemIds[i]];
            IERC721(item.tokenAddress).approve(msg.sender, item.tokenId);
            IERC721(item.tokenAddress).safeTransferFrom(
                address(this),
                trade.from,
                item.tokenId
            );
        }
    }

    function unlockTrade(uint256 tradeId)
        public
        notContract
        whenNotPaused
        whenTradeOpen(tradeId)
        onlyRelevant(tradeId)
    {
        TradeOffer storage trade = trades[tradeId];
        if (msg.sender == trade.from) {
            trade.isFromLocked = false;
        } else {
            trade.isToLocked = false;
        }
        emit Unlock(tradeId, msg.sender);
    }

    function deposit(
        uint256 tradeId,
        address tokenAddress,
        uint256[] memory tokenIds
    )
        public
        notContract
        whenNotPaused
        whenTradeOpen(tradeId)
        onlyRelevant(tradeId)
    {
        uint256[] storage userTradingItemIds = userTradingItems[tradeId][
            msg.sender
        ];
        for (uint256 i = 0; i < tokenIds.length; i++) {
            IERC721(tokenAddress).safeTransferFrom(
                msg.sender,
                address(this),
                tokenIds[i]
            );
            TradeItem memory item = TradeItem(
                tradeId,
                tokenAddress,
                tokenIds[i],
                true
            );
            tradeItems.push(item);
            userTradingItemIds.push(tradeItems.length - 1);
        }
        TradeOffer storage trade = trades[tradeId];
        if (trade.isToLocked) {
            trade.isToLocked = false;
            emit Unlock(tradeId, trade.to);
        }
        if (trade.isFromLocked) {
            trade.isFromLocked = false;
            emit Unlock(tradeId, trade.from);
        }
        emit Deposit(tradeId, msg.sender);
    }

    function withdraw(
        uint256 tradeId,
        address tokenAddress,
        uint256[] memory tokenIds
    )
        public
        notContract
        whenNotPaused
        whenTradeOpen(tradeId)
        onlyRelevant(tradeId)
        nonReentrant
    {
        uint256[] memory userTradingItemIds = userTradingItems[tradeId][
            msg.sender
        ];
        for (uint256 i = 0; i < userTradingItemIds.length; i++) {
            TradeItem storage item = tradeItems[userTradingItemIds[i]];
            if (!item.exists) continue;
            if (item.tokenAddress != tokenAddress) continue;
            if (!_contain(tokenIds, item.tokenId)) continue;
            IERC721(item.tokenAddress).safeTransferFrom(
                address(this),
                msg.sender,
                item.tokenId
            );
            item.exists = false;
        }
        emit Withdraw(tradeId, msg.sender);

        TradeOffer storage trade = trades[tradeId];
        if (trade.isFromLocked) {
            trade.isFromLocked = false;
            emit Unlock(tradeId, trade.from);
        }
        if (trade.isToLocked) {
            trade.isToLocked = false;
            emit Unlock(tradeId, trade.to);
        }
    }

    function withdrawAll(uint256 tradeId)
        public
        notContract
        whenNotPaused
        whenTradeOpen(tradeId)
        onlyRelevant(tradeId)
        nonReentrant
    {
        _withdrawAll(tradeId, msg.sender);
        TradeOffer storage trade = trades[tradeId];
        if (trade.isFromLocked) {
            trade.isFromLocked = false;
            emit Unlock(tradeId, trade.from);
        }
        if (trade.isToLocked) {
            trade.isToLocked = false;
            emit Unlock(tradeId, trade.to);
        }
    }

    function depositedItems(uint256 tradeId, address user)
        public
        view
        returns (TradeItem[] memory)
    {
        uint256[] memory itemIds = userTradingItems[tradeId][user];
        TradeItem[] memory items = new TradeItem[](itemIds.length);
        for (uint256 i = 0; i < itemIds.length; i++) {
            TradeItem memory tradeItem = tradeItems[itemIds[i]];
            items[i] = tradeItem;
        }
        return items;
    }

    function tradesOf(address user) public view returns (TradeOffer[] memory) {
        uint256[] memory userTradeIds = userTrades[user];
        TradeOffer[] memory userOffers = new TradeOffer[](userTradeIds.length);
        for (uint256 i = 0; i < userTradeIds.length; i++) {
            TradeOffer memory trade = trades[userTradeIds[i]];
            userOffers[i] = trade;
        }
        return userOffers;
    }

    function tradeIds(address user) public view returns (uint256[] memory) {
        return userTrades[user];
    }

    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external override returns (bytes4) {
        return
            bytes4(
                keccak256("onERC721Received(address,address,uint256,bytes)")
            );
    }

    function pause() public onlyOwner {
        _pause();
    }

    function unpause() public onlyOwner {
        _unpause();
    }

    function _contain(uint256[] memory list, uint256 element)
        internal
        pure
        returns (bool)
    {
        for (uint256 i = 0; i < list.length; i++) {
            if (element == list[i]) {
                return true;
            }
        }
        return false;
    }

    function _withdrawAll(uint256 tradeId, address to) internal {
        uint256[] memory userTradingItemIds = userTradingItems[tradeId][to];
        for (uint256 i = 0; i < userTradingItemIds.length; i++) {
            TradeItem memory item = tradeItems[userTradingItemIds[i]];
            if (!item.exists) continue;
            IERC721(item.tokenAddress).safeTransferFrom(
                address(this),
                to,
                item.tokenId
            );
            item.exists = false;
        }
        emit Withdraw(tradeId, to);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/IERC721Receiver.sol)

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721Receiver.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}