// SPDX-License-Identifier: MIT
// Roach Racing Club: the first strategic p2e game with deflationary mechanisms (https://roachracingclub.com/)
pragma solidity ^0.8.10;

import "GenomeProviderPolygon.sol";
import "VRFConsumerBaseV2.sol";
import "VRFCoordinatorV2Interface.sol";

/// @title Genome generator
/// @author Shadow Syndicate / Andrey Pelipenko ([email protected])
/// @dev Production version of GenomeProvider that used real Chainlink VRF
contract GenomeProviderChainlink is GenomeProviderPolygon, VRFConsumerBaseV2 {
    bytes32 chainLinkKeyHash;
    uint256 public chainLinkFee;
    uint64 subscriptionId;
    VRFCoordinatorV2Interface vrfCoordinator;
    uint32 constant callbackGasLimit = 2_000_000;
    uint16 constant requestConfirmations = 3;
    mapping(uint => uint) public requests;

    // Chainlink constants: https://docs.chain.link/docs/vrf-contracts/
    constructor(
        address _vrfCoordinator,
        bytes32 _chainLinkKeyHash,
        uint64 _subscriptionId
    )
        VRFConsumerBaseV2(_vrfCoordinator)
    {
        vrfCoordinator = VRFCoordinatorV2Interface(_vrfCoordinator);
        chainLinkKeyHash = _chainLinkKeyHash;
        subscriptionId = _subscriptionId;
    }

    function _requestRandomness(uint tokenId) internal override {
        uint256 requestId = vrfCoordinator.requestRandomWords(
            chainLinkKeyHash,
            subscriptionId,
            requestConfirmations,
            callbackGasLimit,
            1);
        requests[requestId] = tokenId;
    }

    /// @notice ChainlinkVRF callback function with supplied randomness
    function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal override {
        uint tokenId = requests[requestId];
        delete requests[requestId];
        _onRandomnessArrived(tokenId, randomWords[0]);
    }
}

// SPDX-License-Identifier: MIT
// Roach Racing Club: the first strategic p2e game with deflationary mechanisms (https://roachracingclub.com/)
/*
______                 _      ______           _               _____ _       _
| ___ \               | |     | ___ \         (_)             /  __ \ |     | |
| |_/ /___   __ _  ___| |__   | |_/ /__ _  ___ _ _ __   __ _  | /  \/ |_   _| |__
|    // _ \ / _` |/ __| '_ \  |    // _` |/ __| | '_ \ / _` | | |   | | | | | '_ \
| |\ \ (_) | (_| | (__| | | | | |\ \ (_| | (__| | | | | (_| | | \__/\ | |_| | |_) |
\_| \_\___/ \__,_|\___|_| |_| \_| \_\__,_|\___|_|_| |_|\__, |  \____/_|\__,_|_.__/
                                                        __/ |
                                                       |___/
.................................,,:::,...........
..............................,:;;;:::;;;,........
...............,,,,,.........:;;,......,;+:.......
.............:::,,,::,.....,+;,..........:*;......
...........,;:.......,:,..,+:.............:*:.....
..........:;,..........:,.+:...............*+.....
.........,+,..........,,:;+,,,.............;*,....
.........+:.......,:+?SS####SS%*;,.........;*:....
........:+.....,;?S##############S?:.......;*,....
........;+....;?###############%??##+......+*,....
........:+...,%SS?;?#########@@S?++S#:....,+;.....
........,+:..,%S%*,*#####SSSSSS%*;,%S,............
.........;;,..;SS%S#####SSSS%%%?+:*%;.............
..........,....:%########SSS%%?*?%?,..............
.............,,,.+S##@#?+;;*%%SS%;................
.........,,.,+++;:+%##?+*+:,?##S+;:,..............
....,,;+*SS*??***++?S#S?*+:,%S%%%%%?+:,......,....
,:;**???*?#@##S?***++*%%*;,:%%%%**?%%?;,,.,;?%?%??
????*+;:,,*####S%%?*+;:;;,,+#S%%%?*?%??+;*%S?*%SSS
*+;:,....,%@S####SS%?*+:::*[email protected]#%%%%????%%S%*;::,,,:
[email protected]@S%S####S#@%?%%SS#@SS%%%%SS%*++;,......
........,%@@S%%S#@##@#%%%%%%[email protected]##SSS%*+*?%?;,......
........:#@@%%%%%[email protected]##%%%%%%%#@##?++**%%S%+:......
[email protected]@#%SS%%%SSS?S%%%%%%[email protected]?????%?S%*;,.....
[email protected]@@%%%%%%%%%%%%%%%%%%%%%%??**?%#%%;,.....
*/
pragma solidity ^0.8.10;

import "IRoachNFT.sol";
import "Operators.sol";

/// @title Genome generator
/// @author Shadow Syndicate / Andrey Pelipenko ([email protected])
/// @dev Should be deployed on cheap network, like Polygon
///      TokenSeed is generated using formula sha3(tokenId, traitBonus, devSeed, vrfSeed)
///      Where:
///        tokenId - roach id.
///        traitBonus - trait bonus from whitelist, 0 for public sale.
///        devSeed - secret value, that is available only to developer team while NFT is not revealed
///        vrfSeed - random value that is generated by Chainlink VRF after sha3(devSeed) is published.
///      TokenSeed is unpredictable during the mint stage for all parties:
///        Developers can't predict vrfSeed and mintBlockHash
///        Miners can't predict devSeed
///        Buyers can't predict devSeed and vrfSeed
///      So TokenSeed is unpredictable but fixed and can be checked at any time after the reveal.
///      Genome is generated using tokenSeed. For the same tokenSeed, there will be equal genomes.
///      After the genesis sale is finished, the game will publish devSeed, and anyone will be able to
///      check tokenSeeds and corresponding genomes.
///      Genome is signed by the secret private key and transferred from Polygon network to Ethereum
///      as part of the reveal process.
/// @dev GenomeProviderPolygon is used only for testing because of mocked VRF request.
///      GenomeProviderChainlink should be used for production.
contract GenomeProviderPolygon is Operators {

    uint constant TRAIT_COUNT = 6;
    uint constant MAX_BONUS = 25;

    struct TraitConfig {
        uint32 sum;
        uint8[] slots;
        // data format: trait1, color1a, color1b, trait2, color2a, color2b, ...
        uint8[] traitData;
        uint16[] weight;
        uint16[] weightMaxBonus;
    }

    mapping(uint => TraitConfig) public traits; // slot -> array of trait weight

    struct Roach {
        uint256 vrfSeed;
        uint256 tokenSeed;
        uint256 devSeedHash;
        uint256 devSeed;
        bytes genome;
        uint64 revealTime;
        uint8 traitBonus;
        bool requested;
    }

    mapping(uint => Roach) public roach;

    event DevSeedHash(uint indexed tokenId, uint256 devSeedHash);
    event GenomeSaved(uint indexed tokenId, uint devSeed, uint tokenSeed, bytes genome);
    event RevealVrf(uint indexed tokenId, uint vrfSeed);
    event RevealRequest(uint indexed tokenId, uint8 traitBonus, uint8 ownerSigV, bytes32 ownerSigR, bytes32 ownerSigS);

    function _publishDevSeedHash(uint tokenId, uint _devSeedHash) internal {
        Roach storage _roach = roach[tokenId];
        require(_roach.devSeedHash == 0, "Can't call twice");
        _roach.devSeedHash = _devSeedHash;
        emit DevSeedHash(tokenId, _devSeedHash);
    }

    function publishDevSeedHash(uint tokenId, uint _devSeedHash) external onlyOperator {
        _publishDevSeedHash(tokenId, _devSeedHash);
    }

    function publishDevSeedHashBatch(uint startTokenId, uint[] calldata _devSeedHashes) external onlyOperator {
        for (uint i = 0; i < _devSeedHashes.length; i++) {
            _publishDevSeedHash(startTokenId + i, _devSeedHashes[i]);
        }
    }

    function isRevealed(uint tokenId) external view returns (bool) {
        return roach[tokenId].revealTime != 0;
    }

    function getRoach(uint tokenId) external view returns (Roach memory) {
        return roach[tokenId];
    }

    /// @dev Function is used to check tokenSeed generation after devSeed is published
    function calculateTokenSeed(uint tokenId, uint traitBonus, uint devSeed, uint vrfSeed)
        public view returns (uint tokenSeed)
    {
        return uint(keccak256(abi.encodePacked(tokenId, traitBonus, devSeed, vrfSeed)));
    }

    /// @dev Calculates genome for each roach using tokenSeed as seed
    function calculateGenome(uint256 tokenSeed, uint8 traitBonus) external view returns (bytes memory genome) {
        genome = _normalizeGenome(tokenSeed, traitBonus);
    }

    /// @dev Called only after contract is deployed and before genomes are generated
    function requestReveal(uint tokenId, uint8 traitBonus, uint256 devSeedHash,
            uint8 ownerSigV, bytes32 ownerSigR, bytes32 ownerSigS)
        external onlyOperator
    {
        Roach storage _roach = roach[tokenId];
        require(!_roach.requested, "Can't call twice");
        _roach.requested = true;
        _roach.devSeedHash = devSeedHash;
        _roach.traitBonus = traitBonus;
        _requestRandomness(tokenId);
        emit RevealRequest(tokenId, traitBonus, ownerSigV, ownerSigR, ownerSigS);
    }

    /// @dev Stub function for filling random, will be overriden in Chainlink version
    function _requestRandomness(uint tokenId) internal virtual {
        uint256 randomness = uint(keccak256(abi.encodePacked(block.timestamp)));
        _onRandomnessArrived(tokenId, randomness);
    }

    /// @dev Saves Chainlink VRF random value as vrfSeed
    function _onRandomnessArrived(uint tokenId, uint256 _randomness) internal {
        Roach storage _roach = roach[tokenId];
        require(_roach.vrfSeed == 0, "Can't call twice");
        _roach.vrfSeed = _randomness;
        _roach.revealTime = uint64(block.timestamp);
        emit RevealVrf(tokenId, _roach.vrfSeed);
    }

    function calculateDevSeedHash(uint _devSeed) public view returns (uint) {
        return uint(keccak256(abi.encodePacked(_devSeed)));
    }

    function saveGenome(uint tokenId, uint _devSeed) external onlyOperator {
        Roach storage _roach = roach[tokenId];
        require(_roach.devSeed == 0, "Can't call twice");
        require(_roach.vrfSeed != 0, "VRF is set");
        require(_roach.devSeedHash == calculateDevSeedHash(_devSeed), "devSeed hash correct");
        _roach.devSeed = _devSeed;
        _roach.tokenSeed = calculateTokenSeed(tokenId, _roach.traitBonus, _roach.devSeed, _roach.vrfSeed);
        _roach.genome = _normalizeGenome(_roach.tokenSeed, _roach.traitBonus);
        emit GenomeSaved(tokenId, _roach.devSeed, _roach.tokenSeed, _roach.genome);
    }


    /// @dev Setups genome configuration
    function setTraitConfig(
        uint traitIndex,
        uint8[] calldata _slots,
        uint8[] calldata _traitData,
        uint16[] calldata _weight,
        uint16[] calldata _weightMaxBonus
    )
        external onlyOperator
    {
        require(_weight.length == _weightMaxBonus.length, 'weight length mismatch');
        require(_slots.length * _weight.length == _traitData.length, '_traitData length mismatch');

        uint32 sum = 0;
        for (uint i = 0; i < _weight.length; i++) {
            sum += _weight[i];
        }
        traits[traitIndex] = TraitConfig(sum, _slots, _traitData, _weight, _weightMaxBonus);
    }

    function getWeightedRandom(uint traitType, uint randomSeed, uint bonus)
        internal view
        returns (uint choice, uint newRandomSeed)
    {
        TraitConfig storage config = traits[traitType];
        uint div = config.sum * MAX_BONUS;
        uint r = randomSeed % div;
        uint i = 0;
        uint acc = 0;
        while (true) {
            acc += config.weight[i] * (MAX_BONUS - bonus) + (config.weightMaxBonus[i] * bonus);
            if (acc > r) {
                choice = i;
                newRandomSeed = randomSeed / div;
                break;
            }
            i++;
        }
    }

    function _normalizeGenome(uint256 _randomness, uint8 _traitBonus) internal view returns (bytes memory) {

        bytes memory result = new bytes(32);
        result[0] = 0; // version
        for (uint i = 1; i <= TRAIT_COUNT; i++) {
            uint trait;
            (trait, _randomness) = getWeightedRandom(i, _randomness, _traitBonus);
            TraitConfig storage config = traits[i];
            for (uint j = 0; j < config.slots.length; j++) {
                result[config.slots[j]] = bytes1(uint8(config.traitData[trait * config.slots.length + j]));
            }
        }

        TraitConfig storage lastConfig = traits[TRAIT_COUNT];
        uint maxSlot = lastConfig.slots[lastConfig.slots.length - 1];
        for (uint i = maxSlot + 1; i < 32; i++) {
            result[i] = bytes1(uint8(_randomness & 0xFF));
            _randomness >>= 8;
        }
        return result;
    }
}

// SPDX-License-Identifier: MIT
// Roach Racing Club: the first strategic p2e game with deflationary mechanisms (https://roachracingclub.com/)
pragma solidity ^0.8.10;

/// @title Roach Racing Club NFT registry interface
interface IRoachNFT {

    /// @notice Mints new token with autoincremented index and stores traitBonus/syndicate for reveal
    function mintGen0(address to, uint count, uint8 traitBonus, string calldata syndicate) external;

    /// @notice lastRoachId doesn't equap totalSupply because some token will be burned
    ///         in using Run or Die mechanic
    function lastRoachId() external view returns (uint);

    /// @notice Total number of minted tokens for account
    function getNumberMinted(address account) external view returns (uint64);

    function revealOperator(uint tokenId, bytes calldata genome) external;

    function ownerOf(uint256 tokenId) external view returns (address);
}

// SPDX-License-Identifier: MIT
// Roach Racing Club: the first strategic p2e game with deflationary mechanisms (https://roachracingclub.com/)
pragma solidity ^0.8.10;

import "IERC20.sol";
import "Ownable.sol";

/// @title Helper contract for contract maintainance
/// @author Shadow Syndicate / Andrey Pelipenko ([email protected])
contract Operators is Ownable {
    mapping (address=>bool) operatorAddress;

    modifier onlyOperator() {
        require(isOperator(msg.sender), "Access denied");
        _;
    }

    function isOwner(address _addr) public view returns (bool) {
        return owner() == _addr;
    }

    function isOperator(address _addr) public view returns (bool) {
        return operatorAddress[_addr] || isOwner(_addr);
    }

    function _addOperator(address _newOperator) internal {
        operatorAddress[_newOperator] = true;
    }

    function addOperator(address _newOperator) external onlyOwner {
        require(_newOperator != address(0), "New operator is empty");
        _addOperator(_newOperator);
    }

    function removeOperator(address _oldOperator) external onlyOwner {
        delete(operatorAddress[_oldOperator]);
    }

    /**
     * @dev Owner can claim any tokens that are transferred
     * to this contract address
     */
    function withdrawERC20(IERC20 _tokenContract, address _admin) external onlyOwner {
        uint256 balance = _tokenContract.balanceOf(address(this));
        _tokenContract.transfer(_admin, balance);
    }

    function withdrawEther() external onlyOwner {
        payable(msg.sender).transfer(address(this).balance);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.0 (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.0 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.0 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/** ****************************************************************************
 * @notice Interface for contracts using VRF randomness
 * *****************************************************************************
 * @dev PURPOSE
 *
 * @dev Reggie the Random Oracle (not his real job) wants to provide randomness
 * @dev to Vera the verifier in such a way that Vera can be sure he's not
 * @dev making his output up to suit himself. Reggie provides Vera a public key
 * @dev to which he knows the secret key. Each time Vera provides a seed to
 * @dev Reggie, he gives back a value which is computed completely
 * @dev deterministically from the seed and the secret key.
 *
 * @dev Reggie provides a proof by which Vera can verify that the output was
 * @dev correctly computed once Reggie tells it to her, but without that proof,
 * @dev the output is indistinguishable to her from a uniform random sample
 * @dev from the output space.
 *
 * @dev The purpose of this contract is to make it easy for unrelated contracts
 * @dev to talk to Vera the verifier about the work Reggie is doing, to provide
 * @dev simple access to a verifiable source of randomness. It ensures 2 things:
 * @dev 1. The fulfillment came from the VRFCoordinator
 * @dev 2. The consumer contract implements fulfillRandomWords.
 * *****************************************************************************
 * @dev USAGE
 *
 * @dev Calling contracts must inherit from VRFConsumerBase, and can
 * @dev initialize VRFConsumerBase's attributes in their constructor as
 * @dev shown:
 *
 * @dev   contract VRFConsumer {
 * @dev     constructor(<other arguments>, address _vrfCoordinator, address _link)
 * @dev       VRFConsumerBase(_vrfCoordinator) public {
 * @dev         <initialization with other arguments goes here>
 * @dev       }
 * @dev   }
 *
 * @dev The oracle will have given you an ID for the VRF keypair they have
 * @dev committed to (let's call it keyHash). Create subscription, fund it
 * @dev and your consumer contract as a consumer of it (see VRFCoordinatorInterface
 * @dev subscription management functions).
 * @dev Call requestRandomWords(keyHash, subId, minimumRequestConfirmations,
 * @dev callbackGasLimit, numWords),
 * @dev see (VRFCoordinatorInterface for a description of the arguments).
 *
 * @dev Once the VRFCoordinator has received and validated the oracle's response
 * @dev to your request, it will call your contract's fulfillRandomWords method.
 *
 * @dev The randomness argument to fulfillRandomWords is a set of random words
 * @dev generated from your requestId and the blockHash of the request.
 *
 * @dev If your contract could have concurrent requests open, you can use the
 * @dev requestId returned from requestRandomWords to track which response is associated
 * @dev with which randomness request.
 * @dev See "SECURITY CONSIDERATIONS" for principles to keep in mind,
 * @dev if your contract could have multiple requests in flight simultaneously.
 *
 * @dev Colliding `requestId`s are cryptographically impossible as long as seeds
 * @dev differ.
 *
 * *****************************************************************************
 * @dev SECURITY CONSIDERATIONS
 *
 * @dev A method with the ability to call your fulfillRandomness method directly
 * @dev could spoof a VRF response with any random value, so it's critical that
 * @dev it cannot be directly called by anything other than this base contract
 * @dev (specifically, by the VRFConsumerBase.rawFulfillRandomness method).
 *
 * @dev For your users to trust that your contract's random behavior is free
 * @dev from malicious interference, it's best if you can write it so that all
 * @dev behaviors implied by a VRF response are executed *during* your
 * @dev fulfillRandomness method. If your contract must store the response (or
 * @dev anything derived from it) and use it later, you must ensure that any
 * @dev user-significant behavior which depends on that stored value cannot be
 * @dev manipulated by a subsequent VRF request.
 *
 * @dev Similarly, both miners and the VRF oracle itself have some influence
 * @dev over the order in which VRF responses appear on the blockchain, so if
 * @dev your contract could have multiple VRF requests in flight simultaneously,
 * @dev you must ensure that the order in which the VRF responses arrive cannot
 * @dev be used to manipulate your contract's user-significant behavior.
 *
 * @dev Since the block hash of the block which contains the requestRandomness
 * @dev call is mixed into the input to the VRF *last*, a sufficiently powerful
 * @dev miner could, in principle, fork the blockchain to evict the block
 * @dev containing the request, forcing the request to be included in a
 * @dev different block with a different hash, and therefore a different input
 * @dev to the VRF. However, such an attack would incur a substantial economic
 * @dev cost. This cost scales with the number of blocks the VRF oracle waits
 * @dev until it calls responds to a request. It is for this reason that
 * @dev that you can signal to an oracle you'd like them to wait longer before
 * @dev responding to the request (however this is not enforced in the contract
 * @dev and so remains effective only in the case of unmodified oracle software).
 */
abstract contract VRFConsumerBaseV2 {
  error OnlyCoordinatorCanFulfill(address have, address want);
  address private immutable vrfCoordinator;

  /**
   * @param _vrfCoordinator address of VRFCoordinator contract
   */
  constructor(address _vrfCoordinator) {
    vrfCoordinator = _vrfCoordinator;
  }

  /**
   * @notice fulfillRandomness handles the VRF response. Your contract must
   * @notice implement it. See "SECURITY CONSIDERATIONS" above for important
   * @notice principles to keep in mind when implementing your fulfillRandomness
   * @notice method.
   *
   * @dev VRFConsumerBaseV2 expects its subcontracts to have a method with this
   * @dev signature, and will call it once it has verified the proof
   * @dev associated with the randomness. (It is triggered via a call to
   * @dev rawFulfillRandomness, below.)
   *
   * @param requestId The Id initially returned by requestRandomness
   * @param randomWords the VRF output expanded to the requested number of words
   */
  function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal virtual;

  // rawFulfillRandomness is called by VRFCoordinator when it receives a valid VRF
  // proof. rawFulfillRandomness then calls fulfillRandomness, after validating
  // the origin of the call
  function rawFulfillRandomWords(uint256 requestId, uint256[] memory randomWords) external {
    if (msg.sender != vrfCoordinator) {
      revert OnlyCoordinatorCanFulfill(msg.sender, vrfCoordinator);
    }
    fulfillRandomWords(requestId, randomWords);
  }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface VRFCoordinatorV2Interface {
  /**
   * @notice Get configuration relevant for making requests
   * @return minimumRequestConfirmations global min for request confirmations
   * @return maxGasLimit global max for request gas limit
   * @return s_provingKeyHashes list of registered key hashes
   */
  function getRequestConfig()
    external
    view
    returns (
      uint16,
      uint32,
      bytes32[] memory
    );

  /**
   * @notice Request a set of random words.
   * @param keyHash - Corresponds to a particular oracle job which uses
   * that key for generating the VRF proof. Different keyHash's have different gas price
   * ceilings, so you can select a specific one to bound your maximum per request cost.
   * @param subId  - The ID of the VRF subscription. Must be funded
   * with the minimum subscription balance required for the selected keyHash.
   * @param minimumRequestConfirmations - How many blocks you'd like the
   * oracle to wait before responding to the request. See SECURITY CONSIDERATIONS
   * for why you may want to request more. The acceptable range is
   * [minimumRequestBlockConfirmations, 200].
   * @param callbackGasLimit - How much gas you'd like to receive in your
   * fulfillRandomWords callback. Note that gasleft() inside fulfillRandomWords
   * may be slightly less than this amount because of gas used calling the function
   * (argument decoding etc.), so you may need to request slightly more than you expect
   * to have inside fulfillRandomWords. The acceptable range is
   * [0, maxGasLimit]
   * @param numWords - The number of uint256 random values you'd like to receive
   * in your fulfillRandomWords callback. Note these numbers are expanded in a
   * secure way by the VRFCoordinator from a single random value supplied by the oracle.
   * @return requestId - A unique identifier of the request. Can be used to match
   * a request to a response in fulfillRandomWords.
   */
  function requestRandomWords(
    bytes32 keyHash,
    uint64 subId,
    uint16 minimumRequestConfirmations,
    uint32 callbackGasLimit,
    uint32 numWords
  ) external returns (uint256 requestId);

  /**
   * @notice Create a VRF subscription.
   * @return subId - A unique subscription id.
   * @dev You can manage the consumer set dynamically with addConsumer/removeConsumer.
   * @dev Note to fund the subscription, use transferAndCall. For example
   * @dev  LINKTOKEN.transferAndCall(
   * @dev    address(COORDINATOR),
   * @dev    amount,
   * @dev    abi.encode(subId));
   */
  function createSubscription() external returns (uint64 subId);

  /**
   * @notice Get a VRF subscription.
   * @param subId - ID of the subscription
   * @return balance - LINK balance of the subscription in juels.
   * @return reqCount - number of requests for this subscription, determines fee tier.
   * @return owner - owner of the subscription.
   * @return consumers - list of consumer address which are able to use this subscription.
   */
  function getSubscription(uint64 subId)
    external
    view
    returns (
      uint96 balance,
      uint64 reqCount,
      address owner,
      address[] memory consumers
    );

  /**
   * @notice Request subscription owner transfer.
   * @param subId - ID of the subscription
   * @param newOwner - proposed new owner of the subscription
   */
  function requestSubscriptionOwnerTransfer(uint64 subId, address newOwner) external;

  /**
   * @notice Request subscription owner transfer.
   * @param subId - ID of the subscription
   * @dev will revert if original owner of subId has
   * not requested that msg.sender become the new owner.
   */
  function acceptSubscriptionOwnerTransfer(uint64 subId) external;

  /**
   * @notice Add a consumer to a VRF subscription.
   * @param subId - ID of the subscription
   * @param consumer - New consumer which can use the subscription
   */
  function addConsumer(uint64 subId, address consumer) external;

  /**
   * @notice Remove a consumer from a VRF subscription.
   * @param subId - ID of the subscription
   * @param consumer - Consumer to remove from the subscription
   */
  function removeConsumer(uint64 subId, address consumer) external;

  /**
   * @notice Cancel a subscription
   * @param subId - ID of the subscription
   * @param to - Where to send the remaining LINK to
   */
  function cancelSubscription(uint64 subId, address to) external;
}