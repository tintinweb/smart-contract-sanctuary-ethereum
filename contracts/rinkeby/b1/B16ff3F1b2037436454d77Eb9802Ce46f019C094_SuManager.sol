// SPDX-License-Identifier: BSL 1.1

/*
  Copyright 2020 Unit Protocol: Artem Zakharov ([email protected]).
*/
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";

import "../interfaces/ISuOracle.sol";
import "../interfaces/ISuVault.sol";
import "../interfaces/ISuCdpRegistry.sol";
import "../interfaces/ISuManagerParameters.sol";
import "../interfaces/ISuVaultParameters.sol";
import "./SuManagerParameters.sol";

// finally found the managers mighty over the vault
// user does only interact with manager as proxy to the wallet
/// Yes
// there is only one manager for vault each moment
/// I'm sure, the code doesn't enforce this, could be multiple managers
// suggest to maintain only address of active manager instead of managers list
/// not sure why
contract SuManager is ReentrancyGuard, SuManagerParameters {
    ISuVault public immutable vault;
    ISuManagerParameters public immutable managerParameters;
    ISuCdpRegistry public immutable cdpRegistry;

    address public suOracle;

    // 2^112 - what is meaning of 112?
    ///  float standard https://en.wikipedia.org/wiki/Q_(number_format)
    uint public constant Q112 = 2 ** 112;

    // 10^5 = 10000
    uint public constant DENOMINATOR_1E5 = 1e5;

    // even triggered when user deposit collateral
    event Join(address indexed asset, address indexed owner, uint main, uint stablecoin);

    // event triggered when user withdraws collateral
    event Exit(address indexed asset, address indexed owner, uint main, uint stablecoin);

    // event triggered when user didnt withdraw collateral before price goes down
    event LiquidationTriggered(address indexed asset, address indexed owner);

    modifier checkpoint(address asset, address owner) {
        _;
        cdpRegistry.checkpoint(asset, owner);
    }

     // aggregation over composition
    constructor(address _vault, address _suOracle, address _cdpRegistry)
        SuManagerParameters(_vault)
    {
        address _managerParameters = address (this);
        require(
            _managerParameters != address(0) &&
            _cdpRegistry != address(0),
                "Unit Protocol: INVALID_ARGS"
        );

        require(_suOracle != address(0), "INVALID_ARGS: SU_ORACLE");

        // require(ISuVaultManagerParameters(_managerParameters).vaultParameters() != address(0), "INVALID_ARGS: VAULT_PARAMETERS");

        // require(ISuVaultParameters(ISuVaultManagerParameters(_managerParameters).vaultParameters()).vault() != address(0), "INVALID_ARGS: VAULT");

        // require(ISuVault(ISuVaultParameters(ISuVaultManagerParameters(_managerParameters).vaultParameters()).vault()).weth() != address(0), "INVALID_ARGS: VAULT_MANAGER");

        suOracle = _suOracle;

        managerParameters = ISuManagerParameters(_managerParameters);

//        vault = ISuVault(ISuVaultParameters(ISuVaultManagerParameters(_managerParameters).vaultParameters()).vault());
        vault = ISuVault(_vault);

        cdpRegistry = ISuCdpRegistry(_cdpRegistry);
    }

    // wrapper ether only, no native
    receive() external payable {
        revert("Unit Protocol: RESTRICTED");
    }

      // why checkpoint is needed? to check if depositing collateral allowed for user?
      // this function is called by user to deposit collateral and receive stablecoin
      // before calling this function user has to approve the Vault to take his collateral
      /// Yes,
    function join(address asset, uint assetAmount, uint stablecoinAmount) public nonReentrant checkpoint(asset, msg.sender) {
        require(stablecoinAmount != 0 || assetAmount != 0, "Unit Protocol: USELESS_TX");

        require(IERC20Metadata(asset).decimals() <= 18, "Unit Protocol: NOT_SUPPORTED_DECIMALS");

        if (stablecoinAmount == 0) {

            // why user deposit collateral but does not take stablecoin?
            // should be called in separate function
            /// Use doesn't want to get liquidate, so stakes more collateral
            vault.deposit(asset, msg.sender, assetAmount);

        } else {
            bool spawned = vault.debts(asset, msg.sender) != 0;

            if (!spawned) {
                // create a new debt position for user with current oracle
                // why oracles are associated with user?
                /// Historical reasons, now it's deprecated
                // because oracle could have been changed since the position was created by user
                // new positions will be spawned with new oracle but existing ones remain the same
                // Doesn't matter, it's depreacted.
                vault.spawn(asset, msg.sender);
            }

            if (assetAmount != 0) {
                // deposit collateral to the wallet
                vault.deposit(asset, msg.sender, assetAmount);
            }

            // issue stablecoin to the user
            // why stablecoinAmount is chosen by user?
            // it should be calculated from amount of collateral given
            vault.borrow(asset, msg.sender, stablecoinAmount);

            // maybe here we check that stablecoinAmount are corresponding to assetAmount?
            // how does user know correct values? probably by calling helper view function
            _ensurePositionCollateralization(asset, msg.sender);

        }

        emit Join(asset, msg.sender, assetAmount, stablecoinAmount);
    }

      // user can pay back the stablecoin and take his collateral
      // instead of passing both assetAmount and stablecoinAmount
      // better user just to pass one of them
      // also pass preferred rate and maybe acceptable diff percent
      // that's the purpose of passing both to protect user from rate fluctuations
    function exit(address asset, uint assetAmount, uint stablecoinAmount) public nonReentrant checkpoint(asset, msg.sender) returns (uint) {

        // what the case when stablecoinAmount allowed to be zero?
        require(assetAmount != 0 || stablecoinAmount != 0, "Unit Protocol: USELESS_TX");

        uint debt = vault.debts(asset, msg.sender);

        // not to pay more stablecoin than debt
        if (stablecoinAmount > debt) { stablecoinAmount = debt; }

        if (assetAmount == 0) {
            // why pay stablecoin but not withdrawing collateral?
            /// To stop pay interest but have ability to loan in the future
            _repay(asset, msg.sender, stablecoinAmount);
        } else {
            // pay full debt in stablecoin
            if (debt == stablecoinAmount) {
                // vault will transfer collateral to the user
                vault.withdraw(asset, msg.sender, assetAmount);
                if (stablecoinAmount != 0) {
                    // how could it be zero? then debt is zero too
                    /// Yes, if you returned debt in other tx but now want to take your collateral
                    _repay(asset, msg.sender, stablecoinAmount);
                }
            } else {
                // pay partly
                vault.withdraw(asset, msg.sender, assetAmount);

                if (stablecoinAmount != 0) {
                    _repay(asset, msg.sender, stablecoinAmount);
                }

                vault.update(asset, msg.sender);

                // make sure partial repayment is valid
                // but dont need to check this after full repayment?
                /// Yes, because divizion by 0
                _ensurePositionCollateralization(asset, msg.sender);
            }
        }

        emit Exit(asset, msg.sender, assetAmount, stablecoinAmount);

        return stablecoinAmount;
    }

      // alternatively it allowed to pass collateral amount and calculate stablecoin amount
      // how does user calculate repayment value?
      /// UX convenience function
    function exit_targetRepayment(address asset, uint assetAmount, uint repayment) external returns (uint) {

        uint stablecoinAmount = _calcPrincipal(asset, msg.sender, repayment);

        return exit(asset, assetAmount, stablecoinAmount);
    }

    // decrease debt amount by burning repaid stablecoin
    function _repay(address asset, address owner, uint stablecoinAmount) internal {
        // calculate fee
        uint fee = vault.calculateFee(asset, owner, stablecoinAmount);

        // charge fee from the vault
        vault.chargeFee(vault.stablecoin(), owner, fee);

        // burn stablecoin from the vault
        uint debtAfter = vault.repay(asset, owner, stablecoinAmount);
        if (debtAfter == 0) {
            vault.destroy(asset, owner);
        }
    }

    // after partial repayment should be made sure its still collateralized enough
    function _ensurePositionCollateralization(address asset, address owner) internal view {
        // calculate value in usd from collateral position
        uint usdValue_q112 = getCollateralUsdValue_q112(asset, owner);

        // multiply value in usd to the collateral ratio, then divide by 2^112 and divide by 100
        uint usdLimit = usdValue_q112 * managerParameters.initialCollateralRatio(asset) / Q112 / 100;

        // make sure calculated allowed limit is higher than the actual debt
        require(vault.getTotalDebt(asset, owner) <= usdLimit, "Unit Protocol: UNDERCOLLATERALIZED");
    }

    // anyone can trigger liquidation
    // once position happen to be under collaterazied
    function triggerLiquidation(address asset, address owner) external nonReentrant {


        // calculate valut of collateral
        uint usdValue_q112 = getCollateralUsdValue_q112(asset, owner);

        // and check if it can be liquidated
        require(_isLiquidatablePosition(asset, owner, usdValue_q112), "Unit Protocol: SAFE_POSITION");

        // what are the factors discount percent depends upon?
        uint liquidationDiscount_q112 = usdValue_q112 * managerParameters.liquidationDiscount(asset) / DENOMINATOR_1E5;

        // liquidation price is collateral value minus liquidation discount
        uint initialLiquidationPrice = (usdValue_q112 - liquidationDiscount_q112) / Q112;

        // then vault will trigger liquidation and auction begins
        vault.triggerLiquidation(asset, owner, initialLiquidationPrice);

        emit LiquidationTriggered(asset, owner);
    }

    // * Now we return rate and calculate amount here
    function getCollateralUsdValue_q112(address asset, address owner) public view returns (uint) {
        uint256 assetAmount = vault.collaterals(asset, owner);

        uint256 collateralValueUsd_1e18 = ISuOracle(suOracle).getUsdPrice1e18(asset) * assetAmount / 1e18;

        uint256 collateralValueUsd_q112 = collateralValueUsd_1e18 * Q112;

        return collateralValueUsd_q112;
    }

     // is position allowed to be liquidated
    function _isLiquidatablePosition(
        address asset,
        address owner,
        uint usdValue_q112
    ) internal view returns (bool) {
        // calculate current debt to be returned
        uint debt = vault.getTotalDebt(asset, owner);

        if (debt == 0) return false;

        // make sure its not under collaterazied
        // should liquidation ration always be higher than 1 or allowed lower?
        return debt * 100 * Q112 / usdValue_q112 >= managerParameters.liquidationRatio(asset);
    }

     // view function to check if position is liquidatable
    function isLiquidatablePosition(
        address asset,
        address owner
    ) public view returns (bool) {
        uint usdValue_q112 = getCollateralUsdValue_q112(asset, owner);

        return _isLiquidatablePosition(asset, owner, usdValue_q112);
    }

     // view function to show utilization ratio
     // the same function can be used inside of _isLiquidatablePosition
    function utilizationRatio(
        address asset,
        address owner
    ) public view returns (uint) {
        uint debt = vault.getTotalDebt(asset, owner);
        if (debt == 0) return 0;

        uint usdValue_q112 = getCollateralUsdValue_q112(asset, owner);

        return debt * 100 * Q112 / usdValue_q112;
    }

     // calculate liquidation price
     // can be used inside of _isLiquidatablePosition
    function liquidationPrice_q112(
        address asset,
        address owner
    ) external view returns (uint) {

        uint debt = vault.getTotalDebt(asset, owner);
        if (debt == 0) return type(uint256).max;

        uint collateralLiqPrice = debt * 100 * Q112 / (managerParameters.liquidationRatio(asset));

        require(IERC20Metadata(asset).decimals() <= 18, "Unit Protocol: NOT_SUPPORTED_DECIMALS");

        return collateralLiqPrice / vault.collaterals(asset, owner) / 10 ** (18 - IERC20Metadata(asset).decimals());
    }

    /// util function for UX convenience
    function _calcPrincipal(address asset, address owner, uint repayment) internal view returns (uint) {
        uint fee = vault.stabilityFee(asset, owner) * (block.timestamp - vault.lastUpdate(asset, owner)) / 365 days;
        return repayment * DENOMINATOR_1E5 / (DENOMINATOR_1E5 + fee);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity >=0.7.6;

interface ISuOracle {
    /**
     * @notice returns price1e18(assert) such that:
     *   [assetAmount * price1e18(assert) / 1e18 === $$ 1e18] == suUSD
     *   examples:
     *       market price of btc = $30k,
     *       for 0.1 wBTC the unit256 amount is 0.1 * 1e18
     *       0.1 * 1e18 * (price1e18 / 1e18) == $3000 == uint256(3000*1e18)
     *       => price1e18 = 30000 * 1e18;

     *       market price of usdt = $0.97,
     *       for 1 usdt uint256 = 1 * 1e6
     *       so 1*1e6 * price1e18 / 1e18 == $0.97 == uint256(0.97*1e18)
     *       => 1*1e6 * (price1e18 / 1e18) / (0.97*1e18)   = 1
     *       =>  price1e18 = 0.97 * (1e18/1e6) * 1e18
     * @param asset of erc20 token
     * @return price1e18 such as asset.balanceOf() * price1e18 / 1e18 == $$ 1e18
     **/
    function getUsdPrice1e18(address asset) external view returns (uint256);
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.0;

interface ISuVault {
    function DENOMINATOR_1E5 (  ) external view returns ( uint256 );
    function borrow ( address asset, address user, uint256 amount ) external returns ( uint256 );
    function calculateFee ( address asset, address user, uint256 amount ) external view returns ( uint256 );
    function changeOracleType ( address asset, address user, uint256 newOracleType ) external;
    function chargeFee ( address asset, address user, uint256 amount ) external;
    function collaterals ( address, address ) external view returns ( uint256 );
    function debts ( address, address ) external view returns ( uint256 );
    function deposit ( address asset, address user, uint256 amount ) external;
    function destroy ( address asset, address user ) external;
    function getTotalDebt ( address asset, address user ) external view returns ( uint256 );
    function lastUpdate ( address, address ) external view returns ( uint256 );
    function liquidate ( address asset, address positionOwner, uint256 mainAssetToLiquidator, uint256 mainAssetToPositionOwner, uint256 repayment, uint256 penalty, address liquidator ) external;
    function liquidationBlock ( address, address ) external view returns ( uint256 );
    function liquidationFee ( address, address ) external view returns ( uint256 );
    function liquidationPrice ( address, address ) external view returns ( uint256 );
    function oracleType ( address, address ) external view returns ( uint256 );
    function repay ( address asset, address user, uint256 amount ) external returns ( uint256 );
    function spawn ( address asset, address user ) external;
    function stabilityFee ( address, address ) external view returns ( uint256 );
    function tokenDebts ( address ) external view returns ( uint256 );
    function triggerLiquidation ( address asset, address positionOwner, uint256 initialPrice ) external;
    function update ( address asset, address user ) external;
    function stablecoin (  ) external view returns ( address );
    function vaultParameters (  ) external view returns ( address );
    function withdraw ( address asset, address user, uint256 amount ) external;
}

// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

interface ISuCdpRegistry {

    struct CDP {
        address asset;
        address owner;
    }

    function batchCheckpoint ( address[] calldata assets, address[] calldata owners ) external;
    function batchCheckpointForAsset ( address asset, address[] calldata owners ) external;
    function checkpoint ( address asset, address owner ) external;
    function cr (  ) external view returns ( address );
    function getAllCdps (  ) external view returns ( CDP[] memory r );
    function getCdpsByCollateral ( address asset ) external view returns ( CDP[] memory cdps );
    function getCdpsByOwner ( address owner ) external view returns ( CDP[] memory r );
    function getCdpsCount (  ) external view returns ( uint256 totalCdpCount );
    function getCdpsCountForCollateral ( address asset ) external view returns ( uint256 );
    function isAlive ( address asset, address owner ) external view returns ( bool );
    function isListed ( address asset, address owner ) external view returns ( bool );
    function vault (  ) external view returns ( address );
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.0;

interface ISuManagerParameters {
    function devaluationPeriod ( address ) external view returns ( uint256 );
    function initialCollateralRatio ( address ) external view returns ( uint256 );
    function liquidationDiscount ( address ) external view returns ( uint256 );
    function liquidationRatio ( address ) external view returns ( uint256 );
    function setCollateral (
        address asset,
        uint256 stabilityFeeValue,
        uint256 liquidationFeeValue,
        uint256 initialCollateralRatioValue,
        uint256 liquidationRatioValue,
        uint256 liquidationDiscountValue,
        uint256 devaluationPeriodValue,
        uint256 stablecoinLimit
    ) external;
    function setDevaluationPeriod ( address asset, uint256 newValue ) external;
    function setInitialCollateralRatio ( address asset, uint256 newValue ) external;
    function setLiquidationDiscount ( address asset, uint256 newValue ) external;
    function setLiquidationRatio ( address asset, uint256 newValue ) external;
    function vaultParameters (  ) external view returns ( address );
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.0;

interface ISuVaultParameters {
    function canModifyVault ( address ) external view returns ( bool );
    function foundation (  ) external view returns ( address );
    function isManager ( address ) external view returns ( bool );
    function isOracleTypeEnabled ( uint256, address ) external view returns ( bool );
    function liquidationFee ( address ) external view returns ( uint256 );
    function setCollateral ( address asset, uint256 stabilityFeeValue, uint256 liquidationFeeValue, uint256 stablecoinLimit, uint256[] calldata oracles ) external;
    function setFoundation ( address newFoundation ) external;
    function setLiquidationFee ( address asset, uint256 newValue ) external;
    function setManager ( address who, bool permit ) external;
    function setOracleType ( uint256 _type, address asset, bool enabled ) external;
    function setStabilityFee ( address asset, uint256 newValue ) external;
    function setTokenDebtLimit ( address asset, uint256 limit ) external;
    function setVaultAccess ( address who, bool permit ) external;
    function stabilityFee ( address ) external view returns ( uint256 );
    function tokenDebtLimit ( address ) external view returns ( uint256 );
    function vault (  ) external view returns ( address );
    function vaultParameters (  ) external view returns ( address );
}

// SPDX-License-Identifier: BSL 1.1

/*
  Copyright 2020 Unit Protocol: Artem Zakharov ([email protected]).
*/
pragma solidity ^0.8.0;

import "./SuVaultParameters.sol";
import "../access-control/SuAccessControlSingleton.sol";

/**
 * @title managerParameters
 **/
abstract contract SuManagerParameters is SuAuthenticated {
    // map token to initial collateralization ratio; 0 decimals
    mapping(address => uint) public initialCollateralRatio;

    // map token to liquidation ratio; 0 decimals
    mapping(address => uint) public liquidationRatio;

    // map token to liquidation discount; 3 decimals
    mapping(address => uint) public liquidationDiscount;

    // map token to devaluation period in blocks
    mapping(address => uint) public devaluationPeriod;

    SuVaultParameters public vaultParameters;

    constructor(address _vaultParameters) SuAuthenticated(address(SuAuthenticated(_vaultParameters).ACCESS_CONTROL_SINGLETON())) {
        vaultParameters = SuVaultParameters(_vaultParameters);
    }

    function setCollateral(
        address asset,
        uint stabilityFeeValue,
        uint liquidationFeeValue,
        uint initialCollateralRatioValue,
        uint liquidationRatioValue,
        uint liquidationDiscountValue,
        uint devaluationPeriodValue,
        uint stablecoinLimit
    ) external onlyOwner {
        vaultParameters.setCollateral(asset, stabilityFeeValue, liquidationFeeValue, stablecoinLimit);
        setInitialCollateralRatio(asset, initialCollateralRatioValue);
        setLiquidationRatio(asset, liquidationRatioValue);
        setDevaluationPeriod(asset, devaluationPeriodValue);
        setLiquidationDiscount(asset, liquidationDiscountValue);
    }

    /**
     * @notice Only manager is able to call this function
     * @dev Sets the initial collateral ratio
     * @param asset The address of the main collateral token
     * @param newValue The collateralization ratio (0 decimals)
     **/
    function setInitialCollateralRatio(address asset, uint newValue) public onlyOwner {
        require(newValue != 0 && newValue <= 100, "Unit Protocol: INCORRECT_COLLATERALIZATION_VALUE");
        initialCollateralRatio[asset] = newValue;
    }

    /**
     * @notice Only manager is able to call this function
     * @dev Sets the liquidation ratio
     * @param asset The address of the main collateral token
     * @param newValue The liquidation ratio (0 decimals)
     **/
    function setLiquidationRatio(address asset, uint newValue) public onlyOwner {
        require(newValue != 0 && newValue >= initialCollateralRatio[asset], "Unit Protocol: INCORRECT_COLLATERALIZATION_VALUE");
        liquidationRatio[asset] = newValue;
    }

    /**
     * @notice Only manager is able to call this function
     * @dev Sets the liquidation discount
     * @param asset The address of the main collateral token
     * @param newValue The liquidation discount (3 decimals)
     **/
    function setLiquidationDiscount(address asset, uint newValue) public onlyOwner {
        require(newValue < 1e5, "Unit Protocol: INCORRECT_DISCOUNT_VALUE");
        liquidationDiscount[asset] = newValue;
    }

    /**
     * @notice Only manager is able to call this function
     * @dev Sets the devaluation period of collateral after liquidation
     * @param asset The address of the main collateral token
     * @param newValue The devaluation period in blocks
     **/
    function setDevaluationPeriod(address asset, uint newValue) public onlyOwner {
        require(newValue != 0, "Unit Protocol: INCORRECT_DEVALUATION_VALUE");
        devaluationPeriod[asset] = newValue;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: BSL 1.1

import "../access-control/SuAccessControlSingleton.sol";
import "../access-control/SuAuthenticated.sol";

pragma solidity ^0.8.0;

// VaultParameters is Singleton for Access Control
// this looks like configuration contract
// what are the rules to determine these configs for each new allowed collateral?
/// yes, and for all collaterals
// is DAO allowed to choose parameters for existing collaterals?
///
// are there any limits to be enforced? i.e. fee cannot be over 100% percent
/// No, but it's a good idea to have it
abstract contract SuVaultParameters is SuAuthenticated {
    // stability fee can be different for each collateral
    /// yes
    mapping(address => uint) public protocolStabilityFee;

    // liquidation fee too can be different
    /// yes
    mapping(address => uint) public protocolLiquidationFee;

    // map token to USDP mint limit
    /// yes, limit for each collateral-assert
    mapping(address => uint) public tokenDebtLimit;

    // whether an oracle is enabled
    /// TODO:
    mapping(uint => mapping(address => bool)) public isOracleTypeEnabled;

    // what is foundation, DAO?
    /// Beneficiaty as VotingEscrow.vy
    address public foundation;

    address public immutable vault;

    // creator of contract is manager, can it be the same as DAO or can it be removed later?
    /// YES
    // how can vault address be known at this moment?
    /// Precult based on CREATE spec
    // can be created another function to set vault address once deployed?
    /// Yes, possibly with some logic change
    constructor(address _authControl, address payable _vault, address _foundation)
        SuAuthenticated(_authControl)
    {
        require(_vault != address(0), "Unit Protocol: ZERO_ADDRESS");
        require(_foundation != address(0), "Unit Protocol: ZERO_ADDRESS");

        vault = _vault;

//        ISuAccessControl(_authControl).setVault(_vault, true);
//        ISuAccessControl(_authControl).setDAO(msg.sender, true);

        foundation = _foundation;
    }

    // similar function can be added to setVault
    function setFoundation(address newFoundation) external onlyOwner {
        require(newFoundation != address(0), "Unit Protocol: ZERO_ADDRESS");
        foundation = newFoundation;
    }

    // manager is allowed to add new collaterals and modify existing ones
    // I think creating new collaterals and modifying existing ones should be separate functions
    /// Yes, for sercurity reason, it's possible to add events for creating and edititing
    // also different event should be emitted NewCollateral UpdatedCollateral accordingly
    // those events can be handled on frontend to notify user about any changes in rules
    /// Not sure it makes sense to split into create/edit functions
    function setCollateral(
        address asset,
        uint stabilityFeeValue,
        uint liquidationFeeValue,
        uint stablecoinLimit
    ) external onlyOwner {
        // stability fee should be validated in range, what is stability fee should be described here?
        setStabilityFee(asset, stabilityFeeValue);
        // liquidation fee should be validated in range, what is liquidation fee should be explained?
        setLiquidationFee(asset, liquidationFeeValue);
        // why debt limit for collateral is necessary? to manage risks in case of collateral failure?
        setTokenDebtLimit(asset, stablecoinLimit);
    }

    // stability fee is measured as the number of coins per year or percentage?
    // this should be clarified in argument name i.e. stabilityFeePercentageYearly
    /// No, it's APR ( per year, see calculateFee) percentrage, fee percentage; 3 decimals.
    /// YES, self-documented code-style is the best practice.
    function setStabilityFee(address asset, uint newValue) public onlyOwner {
        protocolStabilityFee[asset] = newValue;
    }

    // the same with liquidation fee is not clear
    /// % 0 decimals, needede to get better variable names
    function setLiquidationFee(address asset, uint newValue) public onlyOwner {
        require(newValue <= 100, "Unit Protocol: VALUE_OUT_OF_RANGE");
        protocolLiquidationFee[asset] = newValue;
    }

    // what are allowed types? enum should be defined
    // types out of range should fail transaction
    /// All oracles implementation are numbered, so some of them support this particular asset
    function setOracleType(uint _type, address asset, bool enabled) public onlyOwner {
        isOracleTypeEnabled[_type][asset] = enabled;
    }

    // debt limit can be changed for any collateral along with liquidation and stability fees
    // seems like managers have too much power - that can be dangerous given multiple managers?
    /// Yes, application of  principle of least priviledge needed
    function setTokenDebtLimit(address asset, uint limit) public onlyOwner {
        tokenDebtLimit[asset] = limit;
    }
}

// SPDX-License-Identifier: BSL 1.1

import "@openzeppelin/contracts/access/AccessControl.sol";
import "./SuAuthenticated.sol";

pragma solidity ^0.8.0;

/**
 * @title SuAccessControl
 * @dev Access control for contracts. SuVaultParameters can be inherited from it.
 */
// TODO: refactor by https://en.wikipedia.org/wiki/Principle_of_least_privilege
contract SuAccessControlSingleton is AccessControl, SuAuthenticated {
    /**
     * @dev Initialize the contract with initial owner to be deployer
     */
    constructor() SuAuthenticated(address(this)) {
        _setupRole(DEFAULT_ADMIN_ROLE, msg.sender);
    }

    /**
    * @dev Transfers ownership of the contract to a new account (`newOwner`).
    * Can only be called by the current owner.
    */
    function transferOwnership(address newOwner) external {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        require(hasRole(DEFAULT_ADMIN_ROLE, msg.sender), "Ownable: caller is not the owner");

        if (hasRole(MINTER_ROLE, msg.sender)) {
            grantRole(MINTER_ROLE, newOwner);
            revokeRole(MINTER_ROLE, msg.sender);
        }

        if (hasRole(VAULT_ACCESS_ROLE, msg.sender)) {
            grantRole(VAULT_ACCESS_ROLE, newOwner);
            revokeRole(VAULT_ACCESS_ROLE, msg.sender);
        }

        grantRole(DEFAULT_ADMIN_ROLE, newOwner);
        revokeRole(DEFAULT_ADMIN_ROLE, msg.sender);
    }
}

// SPDX-License-Identifier: BSL 1.1

pragma solidity >=0.7.6;

import "../interfaces/ISuAccessControl.sol";

/**
 * @title SuAuthenticated
 * @dev other contracts should inherit to be authenticated
 */
abstract contract SuAuthenticated {
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant VAULT_ACCESS_ROLE = keccak256("VAULT_ACCESS_ROLE");
    bytes32 private constant DEFAULT_ADMIN_ROLE = 0x00;

    /// @dev the address of SuAccessControlSingleton - it should be one for all contract that inherits SuAuthenticated
    ISuAccessControl public immutable ACCESS_CONTROL_SINGLETON;

    /// @dev should be passed in constructor
    constructor(address _accessControlSingleton) {
        ACCESS_CONTROL_SINGLETON = ISuAccessControl(_accessControlSingleton);
        // TODO: check that _accessControlSingleton points to ISuAccessControl instance
        // require(ISuAccessControl(_accessControlSingleton).supportsInterface(ISuAccessControl.hasRole.selector), "bad dependency");
    }

    /// @dev check DEFAULT_ADMIN_ROLE
    modifier onlyOwner() {
        require(ACCESS_CONTROL_SINGLETON.hasRole(DEFAULT_ADMIN_ROLE, msg.sender), "SuAuth: onlyOwner AUTH_FAILED");
        _;
    }

    /// @dev check VAULT_ACCESS_ROLE
    modifier onlyVaultAccess() {
        require(ACCESS_CONTROL_SINGLETON.hasRole(VAULT_ACCESS_ROLE, msg.sender), "SuAuth: onlyVaultAccess AUTH_FAILED");
        _;
    }

    /// @dev check MINTER_ROLE
    modifier onlyMinter() {
        require(ACCESS_CONTROL_SINGLETON.hasRole(MINTER_ROLE, msg.sender), "SuAuth: onlyMinter AUTH_FAILED");
        _;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (access/AccessControl.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";
import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(uint160(account), 20),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.7.6;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface ISuAccessControl {
    function hasRole(bytes32 role, address account) external view returns (bool);
    function getRoleAdmin(bytes32 role) external view returns (bytes32);
    function grantRole(bytes32 role, address account) external;
    function revokeRole(bytes32 role, address account) external;
    function renounceRole(bytes32 role, address account) external;

    function supportsInterface(bytes4 interfaceId) external view returns (bool);

    // TODO: remove legacy functionality
    function setVault(address _vault, bool _isVault) external;
    function setCdpManager(address _cdpManager, bool _isCdpManager) external;
    function setDAO(address _dao, bool _isDAO) external;
    function setManagerParameters(address _address, bool _permit) external;
    function transferOwnership(address newOwner) external;
}